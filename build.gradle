group 'com.chrisgahlert'
version '0.4.0'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.1"
    }
}

apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'idea'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenCentral()
}

configurations {
    all {
        resolutionStrategy {
            force "org.codehaus.groovy:groovy-all:${GroovySystem.getVersion()}"
        }
    }
}

dependencies {
    compile gradleApi()
    compile 'com.google.inject:guice:4.0'

    testCompile 'com.netflix.nebula:nebula-test:3.1.0'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

pluginBundle {
    website = vcsUrl = 'http://github.com/chrisgahlert/gradle-guice-plugin'
    description = 'Plugin for using Guice dependency injection in plugin development'
    tags = ['guice', 'dependency', 'injection', 'plugin', 'development']

    plugins {
        guicePlugin {
            id = 'com.chrisgahlert.gradle-guice-plugin'
            displayName = 'Gradle Guice Plugin (dummy)'
        }
    }
}
publishPlugins.dependsOn test
